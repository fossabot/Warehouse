/*
 * Copyright 2014-2020 JetBrains s.r.o and contributors. Use of this source code is governed by the Apache 2.0 license.
 */

apply from: rootProject.file('gradle/jdk.gradle')

def jdk = 6

if (project.name in project.ext.jdk11Modules) {
    jdk = 11
} else if (project.name in project.ext.jdk8Modules) {
    jdk = 8
} else if (project.name in project.ext.jdk7Modules) {
    jdk = 7
}

String lookupJdk(int startingFrom) {
    return (startingFrom..20)
        .collect { it < 10 ? "JDK_1_$it" : "JDK_$it" }
        .collect { System.env[it.toString()] }
        .find { it != null && new File(it).exists() }
}

def jdkHome = lookupJdk(jdk)

int currentJdk = ext.currentJdk

apply plugin: "kotlinx-atomicfu"

kotlin {
    jvm {
        compilations.all {
            if (rootProject.ext.jvm_ir_enabled) {
                kotlinOptions.useIR = true
            }

            kotlinOptions.freeCompilerArgs += ["-XXLanguage:+InlineClasses", "-Xuse-14-inline-classes-mangling-scheme"]
        }
    }

    task jarTest(type: Jar, dependsOn: jvmTestClasses) {
        from targets.jvm.compilations.test.output
        classifier 'test'
    }

    configure(targets) {
        compilations.all { compilation ->
            if (compilation.name == "main") {
                def compileTask = tasks.getByName(compilation.compileKotlinTaskName)
                if (!compileTask.name.endsWith("Jvm")) return

                if (jdkHome != null) {
                    compileTask.kotlinOptions.jdkHome = jdkHome
                }
            }
        }
    }

    sourceSets {
        jvmMain.dependencies {
            api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
            if (jdk > 6) {
                api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7', version: kotlin_version
            }
            if (jdk > 7) {
                api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
            }

            api group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        }

        jvmTest.dependencies {
            api 'org.jetbrains.kotlin:kotlin-test'
            api 'org.jetbrains.kotlin:kotlin-test-junit'
            api "junit:junit:$junit_version"

            api group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
            api group: 'junit', name: 'junit', version: junit_version

            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:$coroutines_version"
        }
    }
}

dependencies {
    if (jdk > 7) {
        jvmMainApi(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: coroutines_version) {
            exclude(module: 'kotlin-stdlib')
            exclude(module: 'kotlin-stdlib-jvm')
            exclude(module: 'kotlin-stdlib-jdk8')
            exclude(module: 'kotlin-stdlib-jdk7')
        }
    }
}

jvmTest {
    ignoreFailures = true
    maxHeapSize = "2g"
    exclude("**/*StressTest*")
}

task stressTest(type: Test) {
    classpath = files { jvmTest.classpath }
    testClassesDirs = files { jvmTest.testClassesDirs }

    ignoreFailures = true
    maxHeapSize = "2g"
    forkEvery = 1
    include("**/*StressTest*")
    systemProperty "enable.stress.tests", "true"
}

configurations {
    testOutput.extendsFrom(testCompile)
    boot
}

jvmJar {
    manifest {
        attributes("Implementation-Title": project.name, "Implementation-Version": configuredVersion)
    }
}
